# Fig pre block. Keep at the top of this file.
#[[ -f "$HOME/.fig/shell/zshrc.pre.zsh" ]] && . "$HOME/.fig/shell/zshrc.pre.zsh"

# Claude Code optimizations - skip heavy loading when in Claude environment
if [[ -n "${CLAUDECODE}" ]]; then
    # Minimal setup for Claude Code
    export ZSH=$HOME/.dotfiles
    export PROJECTS=~/Code
    [[ -a ~/.localrc ]] && source ~/.localrc
    [[ -a ~/.aliases ]] && source ~/.aliases
    [[ -a ~/.profile ]] && source ~/.profile
    
    # Basic prompt
    PS1='%n@%m:%~$ '
    
    # Skip heavy initialization and return early
    return
fi

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
if [[ -d "$ZSH" ]]; then
    typeset -U config_files
    config_files=($ZSH/**/*.zsh)

    # load the path files
    for file in ${(M)config_files:#*/path.zsh}
    do
      [[ -f "$file" ]] && source $file
    done

    # load everything but the path and completion files
    for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
    do
      [[ -f "$file" ]] && source $file
    done

    # initialize autocomplete here, otherwise functions won't be loaded
    autoload -U compinit
    compinit

    # load every completion after autocomplete loads
    for file in ${(M)config_files:#*/completion.zsh}
    do
      [[ -f "$file" ]] && source $file
    done

    unset config_files
fi

# Better history

# access global and local history for each shell
# https://superuser.com/questions/446594/separate-up-arrow-lookback-for-local-and-global-zsh-history
up-line-or-local-history() {
    zle set-local-history 1
    zle up-line-or-history
    zle set-local-history 0
}
zle -N up-line-or-local-history
down-line-or-local-history() {
    zle set-local-history 1
    zle down-line-or-history
    zle set-local-history 0
}
zle -N down-line-or-local-history

# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
autoload -U up-line-or-beginning-search
autoload -U down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search

bindkey "^[0A" up-line-or-local-history
bindkey "^[0B" down-line-or-local-history
bindkey "^[[1;5A" up-line-or-beginning-search # [CTRL] + Cursor up
bindkey "^[[1;5B" down-line-or-beginning-search # [CTRL] + Cursor down



export ZSH=$HOME/.oh-my-zsh
[[ -f "$ZSH/oh-my-zsh.sh" ]] && source $ZSH/oh-my-zsh.sh

# Themes
#[[ -f "$HOME/.dotfiles/zsh/themes" ]] && . "$HOME/.dotfiles/zsh/themes"

# Uncomment the following line if you want to change the command execution time
# stamp shown in the history command output.
# You can set one of the optional three formats:
# "mm/dd/yyyy"|"dd.mm.yyyy"|"yyyy-mm-dd"
# or set a custom format using the strftime function format specifications,
# see 'man strftime' for details.
HIST_STAMPS="dd/mm/yyyy"

# Which plugins would you like to load?
# Standard plugins can be found in $ZSH/plugins/
# Custom plugins may be added to $ZSH_CUSTOM/plugins/
# Example format: plugins=(rails git textmate ruby lighthouse)
# Add wisely, as too many plugins slow down shell startup.
plugins=(git fasd history-substring-search shrink-path)

[[ -f ~/.aliases ]] && source ~/.aliases

[[ -f ~/.gitalias ]] && source ~/.gitalias

[[ -f ~/.profile ]] && source ~/.profile

# Better search with skim - credits to https://tratt.net/laurie/blog/2025/better_shell_history_search.html
# NOTE if you get a systime error on mac: brew install gawk (mac awk doesnt support this) and replace awk with gawk in script
setopt EXTENDED_HISTORY
setopt inc_append_history_time
skim-history-widget() {
  local selected num
  setopt localoptions noglobsubst noposixbuiltins pipefail no_aliases 2> /dev/null
  local awk_filter='{ cmd=$0; sub(/^\s*[0-9]+\**\s+/, "", cmd); if (!seen[cmd]++) print $0 }'  # filter out duplicates
  local n=1 fc_opts=''
  if [[ -o extended_history ]]; then
    awk_filter='
{
  ts = int($2)
  delta = systime() - ts
  delta_days = int(delta / 86400)
  # Use timestamp directly for sorting (older = smaller number)
  sort_key = sprintf("%010d", ts)
  if (delta < 0) { $2="+" (-delta_days) "d" }
  else if (delta_days < 1 && delta < 72000) { $2=strftime("%H:%M", ts) }
  else if (delta_days == 0) { $2="1d" }
  else { $2=delta_days "d" }
  line=$0; $1=""; $2=""
  if (!seen[$0]++) print sort_key " " line
}'
    fc_opts='-i'
    n=2
  fi
  selected=( $(fc -rl $fc_opts -t '%s' 1 | sed -E "s/^ *//" | gawk "$awk_filter" | sort -nr | 
    SKIM_DEFAULT_OPTIONS="--height ${SKIM_TMUX_HEIGHT:-40%} $SKIM_DEFAULT_OPTIONS --with-nth $((n+1)).. --no-sort $SKIM_CTRL_R_OPTS --query=${(qqq)LBUFFER} --no-multi" $(__skimcmd)) )
  local ret=$?
  if [ -n "$selected" ]; then
    # Extract command by removing the first 3 fields (timestamp, history#, time/date)
    local cmd=$(echo "$selected" | awk '{$1=$2=$3=""; print $0}' | sed 's/^[ ]*//')
    if [ -n "$cmd" ]; then
      BUFFER=$cmd
      CURSOR=$#BUFFER
    fi
  fi
  zle reset-prompt
  return $ret
}
zle     -N   skim-history-widget
bindkey '^R' skim-history-widget

# Enable vim mode
bindkey -v

# Set vim mode keybindings
bindkey '^P' up-history
bindkey '^N' down-history
bindkey '^?' backward-delete-char
bindkey '^h' backward-delete-char
bindkey '^w' backward-kill-word
# Keep the skim history search (overrides default vim ^r)
bindkey '^r' skim-history-widget

# Improve vim mode cursor indication
function zle-keymap-select {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then
    echo -ne '\e[1 q'
  elif [[ ${KEYMAP} == main ]] ||
       [[ ${KEYMAP} == viins ]] ||
       [[ ${KEYMAP} = '' ]] ||
       [[ $1 = 'beam' ]]; then
    echo -ne '\e[5 q'
  fi
}
zle -N zle-keymap-select
zle-line-init() {
    echo -ne "\e[5 q"
}
zle -N zle-line-init

# Reduce key delay for mode switching
export KEYTIMEOUT=1

# Skip starship in Claude Code for faster startup
if [[ -z "${CLAUDECODE}" ]] && command -v starship >/dev/null 2>&1; then
    eval "$(starship init zsh)"
fi


export CONDA_HOME="$HOME/.miniconda"  # or "$HOME/anaconda3" depending on your installation

# Add Conda to PATH
export PATH="$CONDA_HOME/bin:$PATH"

# Initialize Conda for Zsh (skip warning in Claude Code)
if [[ -f "$CONDA_HOME/etc/profile.d/conda.sh" ]]; then
    . "$CONDA_HOME/etc/profile.d/conda.sh"
elif [[ -z "${CLAUDECODE}" ]]; then
    echo "Warning: Conda initialization script not found"
fi


# Added by Antigravity
export PATH="/Users/michaes/.antigravity/antigravity/bin:$PATH"
